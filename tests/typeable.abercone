
@def sum :: int -> int -> int;
@def sum x y = x + y;

@def diff :: int -> int -> int;
@def diff x y = x - y;

@def mul :: int -> int -> int;
@def mul x y = x * y;

@def apply :: (int -> int -> int) -> int -> int -> int;
@def apply fn x y = fn x y;

@def r1 = apply sum 3 4;

assertEqual (sum  3 4) 7;
assertEqual (diff 10 5) 5;

@def id :: a -> a;
@def id x = x;

@def intId :: int -> int;
@def intId x = x;


@def double = mul 2;

@def dlit :: int -> int;
@def dlit x = 2 * x;

@def r11 = intId 3;

@def r2 = dlit (intId 3);

@def r3 = sum (id 3) (id 4);


@def r4 = id (double (id 3));

@def tList = 2::1::nil;
@def r5 = head tList;

@def r6 = tail tList;


@def r8 = (dlit (head tList)) :: (tail tList);

assertEqual r8 (4::1::nil);

@def map :: (a -> b) -> [a] -> [b];
@def map fn l =
    @if (l == nil) l
    @else   (fn (head l)) ::
            (map fn (tail l));

@def aList = 4::3::2::1::nil;

@def square :: int -> int;
@def square x = x * x;

@def sList = (map square aList);

assertEqual sList (16::9::4::1::nil);

@def sort :: [a] -> [a];
@def sort l =
{
    @def toList :: a -> [a];
    @def toList a = a::nil;

    @def elsAsLists :: [a] -> [[a]];
    @def elsAsLists l = map toList l;


    @def mergePair :: [a] -> [a] -> [a];
    @def mergePair l1 l2 =
    {
        // Wouldn't it be nice to have pattern matching?
        @if ((l1 == nil) && (l2 == nil)) nil
        @else @if (l1 == nil) ((head l2) :: (mergePair l1 (tail l2)))
        @else @if (l2 == nil) ((head l1) :: (mergePair (tail l1) l2))
        @else @if ((head l1) < (head l2)) ((head l1) :: (mergePair (tail l1) l2))
        @else ((head l2) :: (mergePair l1 (tail l2)))
    };

    @def mergeAdjacentPairs :: [[a]] -> [[a]];
    @def mergeAdjacentPairs l =
    {
        @if (l==nil) nil
        @else
        {
            @def first = head l;
            @def tail1 = tail l;
            
            @if (tail1 == nil) first :: nil
            @else (mergePair first (head tail1)) :: (mergeAdjacentPairs (tail tail1))
        }
    };

    @def mergeSortRec :: [[a]] -> [[a]];
    @def mergeSortRec l = @if ((tail l) == nil) l @else (mergeSortRec (mergeAdjacentPairs l));
    
    head (mergeSortRec (elsAsLists l))
};

@def unsorted = 5::6::4::3::7::9::8::2::1::nil;
@def sorted = (sort unsorted);

assertEqual sorted (1::2::3::4::5::6::7::8::9::nil);

print (toString sorted);

//assertEqual boo booRes;




//assertEqual (sum 4 5) 9

